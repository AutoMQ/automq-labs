set working-directory := ".."
SCHEMA_REGISTRY_URL := "http://schema-registry:8081"
NEW_PARTITION_BY := "[month(ts), bucket(id, 16)]"

default: help

help:
    @echo "Partition Scenario (Avro by_schema_id + partition.by)"
    @echo "  create-topic [topic]                 default: order-with-ts"
    @echo "  send-auto [topic] [count]            default: order-with-ts 10"
    @echo "  show-topic-config [topic]            default: order-with-ts"
    @echo "  show-partitions [table]              default: order-with-ts"
    @echo "  show-ddl [table]                     default: order-with-ts"
    @echo "  show-snapshots [table]               default: order-with-ts"
    @echo "  show-history [table]                 default: order-with-ts"
    @echo "  show-files [table]                   default: order-with-ts"
    @echo "  show-manifests [table]               default: order-with-ts"
    @echo "  add-iceberg-partition [topic]        evolve Iceberg partition spec"
    @echo "  query [table]                        default: order-with-ts"
    @echo "  test                                 run full scenario test"

create-topic topic='order-with-ts':
    docker exec automq /opt/automq/kafka/bin/kafka-topics.sh --bootstrap-server localhost:9092 \
      --create --topic {{topic}} --partitions 16 \
      --config automq.table.topic.enable=true \
      --config automq.table.topic.commit.interval.ms=1000 \
      --config automq.table.topic.convert.value.type=by_schema_id \
      --config automq.table.topic.transform.value.type=flatten \
      --config automq.table.topic.namespace=default \
      --config automq.table.topic.partition.by='[month(ts)]' || true

send-auto topic='order-with-ts' count='10':
    @echo "Producing {{count}} Avro messages with ts to {{topic}}"
    SCHEMA_REGISTRY_URL={{SCHEMA_REGISTRY_URL}} bash partition-scenario/scripts/produce-partitioned-auto.sh {{topic}} {{count}}

show-partitions table='order-with-ts':
    @just trino-sql 'SELECT * FROM iceberg.default."{{table}}$partitions" LIMIT 50'

show-ddl table='order-with-ts':
    @just trino-sql 'SHOW CREATE TABLE iceberg.default."{{table}}"'

show-snapshots table='order-with-ts':
    @just trino-sql 'SELECT * FROM iceberg.default."{{table}}$snapshots" ORDER BY committed_at DESC LIMIT 20'

show-history table='order-with-ts':
    @just trino-sql 'SELECT * FROM iceberg.default."{{table}}$history" ORDER BY made_current_at DESC LIMIT 20'

show-files table='order-with-ts':
    @just trino-sql 'SELECT content, file_format, record_count, file_size_in_bytes, file_path FROM iceberg.default."{{table}}$files" LIMIT 50'

show-manifests table='order-with-ts':
    @just trino-sql 'SELECT * FROM iceberg.default."{{table}}$manifests" LIMIT 50'

add-iceberg-partition topic='order-with-ts':
    @echo "Altering Iceberg partition spec for {{topic}} to {{NEW_PARTITION_BY}}..."
    docker exec automq /opt/automq/kafka/bin/kafka-configs.sh --bootstrap-server localhost:9092 \
      --entity-type topics --entity-name {{topic}} \
      --alter --add-config automq.table.topic.partition.by='{{NEW_PARTITION_BY}}'

query table='order-with-ts':
    @just trino-sql 'SELECT * FROM iceberg.default."{{table}}" LIMIT 50'

show-topic-config topic='order-with-ts':
    docker exec automq /opt/automq/kafka/bin/kafka-configs.sh --bootstrap-server localhost:9092 \
      --entity-type topics --entity-name {{topic}} --describe

# Test scenario - runs the complete partition scenario workflow
test:
    @echo "Starting Partition Scenario Test..."
    @just up
    @echo "Step 1: Create Table Topic"
    @just -f partition-scenario/justfile create-topic
    @sleep 5

    @echo "Step 2: Produce Initial Data"
    @just -f partition-scenario/justfile send-auto
    @sleep 10

    @echo "Step 3: Inspect Current Partitioning & Table Info"
    @just -f partition-scenario/justfile show-topic-config
    @just -f partition-scenario/justfile show-partitions
    @just -f partition-scenario/justfile show-ddl

    @echo "Step 4: Evolve Partition Spec"
    @just -f partition-scenario/justfile add-iceberg-partition
    @sleep 8

    @echo "Step 5: Produce More Data"
    @just -f partition-scenario/justfile send-auto
    @sleep 15

    @echo "Step 6: Inspect After Change"
    @just -f partition-scenario/justfile show-topic-config
    @just -f partition-scenario/justfile show-partitions
    @just -f partition-scenario/justfile show-ddl
    @sleep 10

    @echo "Step 7: Query Data"
    @just -f partition-scenario/justfile query

    @echo "Partition Scenario Test Completed Successfully"
    @just down
